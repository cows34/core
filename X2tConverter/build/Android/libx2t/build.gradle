apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    buildToolsVersion '29.0.0'
    compileSdkVersion 29

    defaultConfig {

        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'

        buildConfigField "String", "LIB_X2T", "\"$NAME_LIB\""

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_STL=c++_static",
                          "-DANDROID_ARM_NEON=TRUE",
                          "-DARG_PATH_LIB_SRC_3D_PARTY=${getProjectPath(PATH_LIB_SRC_3D_PARTY)}",
                          "-DARG_PATH_LIB_DST=${getProjectPath(PATH_LIB_DST, true)}",
                          "-DARG_PATH_SRC_CORE=${getProjectPath(PATH_SRC_CORE)}",
                          "-DARG_NAME_LIB=$NAME_LIB"
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            java.srcDirs = [
                "src/main/java"
            ]

            jni.srcDirs = [
                "src/main/cpp"
            ]

            jniLibs.srcDirs = [
                PATH_LIB_DST
            ]
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst "lib/x86_64/lib${NAME_LIB}.so"
        pickFirst "lib/armeabi/lib${NAME_LIB}.so"
        pickFirst "lib/x86/lib${NAME_LIB}.so"
        pickFirst "lib/armeabi-v7a/lib${NAME_LIB}.so"
        pickFirst "lib/arm64-v8a/lib${NAME_LIB}.so"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50"
}



/*
* Check the path for existence,
* otherwise get the path relative to the current project,
* otherwise the error is in the path
* */
def getProjectPath(String path, Boolean isRelativeCreate = false) {
    def absolutePath = file(path)
    def relativePath = file("${file('.').absolutePath}/$path")
    //def relativePath = file("${rootProject.projectDir}/path")

    if (absolutePath.exists() && absolutePath.isDirectory()) {
        return absolutePath.toString().replaceAll("\\\\", "/")
    } else if ((relativePath.exists() && relativePath.isDirectory()) || (isRelativeCreate && relativePath.mkdirs())) {
        return relativePath.toString().replaceAll("\\\\", "/")
    }

    throw new GradleException("getProjectPath($path) - path doesn't exist...")
}